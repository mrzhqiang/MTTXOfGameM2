[@仓库标准配置头]
{
(@storage @getback )
%100
[@main]
#CALLX [\\游戏配置\NPC配置\所有NPC调用头内容.txt] @系统_所有NPC头内容

[@系统_所有NPC头_返回]
#CALLX [\\游戏配置\NPC配置\所有NPC调用头内容.txt] @系统_交易NPC头内容

[@系统_交易NPC头_返回]
}

[@仓库标准配置无任务]
{
{<$SELFNAME>=FCO=243}：\
　需要帮忙吗？目前不收保管费。\ \
<&打开仓库/@storage>\
<&修改仓库密码/@Storagepass>\
<&租用仓库/@仓库_租用仓库>\
<&金融业务/@仓库_金融业务>\
<&关闭/@exit>\
}

[@仓库标准配置尾]
{
[@仓库_租用仓库]
{<$SELFNAME>=FCO=243}：\
　您是不是觉得仓库容量不够用呢？我们\
现在推出{买一送一=FCO=251}活动，只要您租用一个\
仓库，我们将再加送您一个仓库。\
　价格：{[$租用仓库费用]=FCO=251}元宝/{30=FCO=251}天 (可重复购买)\ \

　您当前额外仓库到期时间为：\
　仓库二：{<$STORAGE2STATE>=FCO=250}\
　仓库三：{<$STORAGE3STATE>=FCO=250}\ \

<&租用额外仓库/@仓库_租用仓库_租用>\
<&返回/@系统_交易NPC头_返回>\

[@仓库_租用仓库_租用]
#if
CHECKGAMEGOLD ? [$租用仓库费用] 
#act
GAMEGOLD - [$租用仓库费用]  
STORAGETIMECHANGE 2 + [$租用仓库时间]
STORAGETIMECHANGE 3 + [$租用仓库时间]
MESSAGEBOX 恭喜您，成功的为额外仓库增加了[$租用仓库时间]天时间
GOTO @仓库_租用仓库
#elseact
MESSAGEBOX 对不起，您的[$元宝名称]不够支付本次费用！

[@仓库_金融业务]
{<$SELFNAME>=FCO=243}：\
　在这里我可以帮你将金币转换为金条，\
将金条转换为金砖，请问您需要什么业务？\ \
　100W金币 = 1根金条\
　5根金条  = 1块金砖\ \
<&对换金条/@仓库_金融业务_对换金条>\
<&对换金砖/@仓库_金融业务_对换金砖>\
<&返回/@系统_交易NPC头_返回>\

[@仓库_金融业务_对换金条]
#if
CHECKGOLD 1000000
#act
TAKE 金币 1000000
GIVE 金条 1
MESSAGEBOX 对换成功！
#elseact
MESSAGEBOX 对不起，您的金币不够啊！

[@仓库_金融业务_对换金砖]
#if
CHECKITEMCOUNT 金条 ? 5
#act
TAKECOUNT 金条 5
GIVE 金砖 1
MESSAGEBOX 对换成功！
#elseact
MESSAGEBOX 对不起，您的金条不够啊！
}
