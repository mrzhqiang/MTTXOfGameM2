[@李益仙_检测]
{
#IF
CHECKMISSION [135] 1
CHECKMISSION [136] 0
#ACT
INC S0 <&武馆教头的心事{【任务】=FCO=251}/@任务_教头心事_第二步>\

#IF
CHECKMISSION [141] 1
CHECKMISSION [142] 0
CHECKITEMCOUNT 女儿红酒 ? 1
#ACT
INC S0 <&武馆教头的心事{【任务】=FCO=251}/@任务_教头心事_第七步完成>\

#IF
CHECKMISSION [142] 1
CHECKMISSION [143] 0
#ACT
INC S0 <&武馆教头的心事{【任务】=FCO=251}/@任务_教头心事_第八步接受>\

#IF
CHECKMISSION [143] 1
CHECKMISSION [144] 0
CHECKITEMCOUNT 忍冬滕 ? 10
#ACT
INC S0 <&武馆教头的心事{【任务】=FCO=251}/@任务_教头心事_第八步完成>\
}

[@李益仙_脚本]
{
[@任务_教头心事_第二步]
#IF
CHECKMISSION [135] 1
CHECKMISSION [136] 0
#SAY
{<$SELFNAME>=FCO=243}：\
　　是{武馆教头=FCO=251}要你来找我的？\
　　说实话，让我帮他记起睡梦中的事情\
其实并不难，如果你也能帮我做一件事的\
话，我会帮你这个忙。\ \
<&你说吧，什么事情/@任务_教头心事_第一步完成>\
<&结束对话/@exit>\

[@任务_教头心事_第一步完成]
#IF
CHECKMISSION [135] 1
CHECKMISSION [136] 0
#SAY
{<$SELFNAME>=FCO=243}：\
　　上次跟{龙五大爷=FCO=251}在一起的时候跟他打\
赌，赌金是一坛上好{女儿红酒=FCO=245}，结果他输\
给了我，说好上个月会把酒送给来的，可\
是到现在也没见他踪影。\
　　愿赌就要服输嘛，你说对不对？\
　　你去帮我问问他，是不是他想赖这笔\
帐，如果他想赖帐的话，我跟他没完。\ \
{任务奖励：=FCO=243}\
　声望值： [$任务_教头心事_第二步奖励声望]\
　经验值： [$任务_教头心事_第二步奖励经验]\ \
<&我帮你去问问/@move(比奇省,302,219)>\
<&结束对话/@exit>\
#ACT
SHOWEFFECT [$特效_步骤完成]
SETMISSION [136] 1
CHANGEEXP + [$任务_教头心事_第一步奖励经验]
CREDITPOINT + [$任务_教头心事_第一步奖励声望]
UPDATEMISSION 0 教头心事第二步

[@任务_教头心事_第七步完成]
#IF
CHECKMISSION [141] 1
CHECKMISSION [142] 0
CHECKITEMCOUNT 女儿红酒 ? 1
#SAY
{<$SELFNAME>=FCO=243}：\
　　年轻人，你还真有能耐，能将那五大\
三粗的龙五搞定，并将他欠我的女儿红酒\
给追回来，看来我是小看你了。\
　　至于你说的那个武馆教头的事情，我\
可以帮你配制一颗{如梦丹=FCO=250}，服用后第二天\
肯定能够记起梦中的情景。\
　　但是很不巧，配制如梦丹的主要材料\
<忍冬滕>我这没有了。\ \
<&那应该怎么办呢/@任务_教头心事_第八步接受>\
<&结束对话/@exit>\
#ACT
SHOWEFFECT [$特效_步骤完成]
SETMISSION [142] 1
TAKECOUNT 女儿红酒 1
CHANGEEXP + [$任务_教头心事_第七步奖励经验]
CREDITPOINT + [$任务_教头心事_第七步奖励声望]
DYNAMICGIVE [$任务_教头心事_第七步奖励物品] 2
CHANGEGIVEITEM [$物品_改变属性_绑定状态] [$物品_改变属性_绑定状态_不可交易] [$物品_改变属性_绑定状态_绑定]
CHANGEGIVEITEM [$物品_改变属性_绑定状态] [$物品_改变属性_绑定状态_永不掉落] [$物品_改变属性_绑定状态_绑定]
CHANGEGIVEITEM [$物品_改变属性_绑定状态] [$物品_改变属性_绑定状态_不可出售] [$物品_改变属性_绑定状态_绑定]
CHANGEGIVEITEM [$物品_改变属性_绑定状态] [$物品_改变属性_绑定状态_丢弃消失] [$物品_改变属性_绑定状态_绑定]
CHANGEGIVEITEM [$物品_改变属性_刷新物品]
DELMISSION 0

[@任务_教头心事_第八步接受]
#IF
CHECKMISSION [142] 1
CHECKMISSION [143] 0
#SAY
{<$SELFNAME>=FCO=243}：\
　　在比奇的树林里生长着一些{忍冬滕=FCO=250}，\
但我已年迈，不能为你去采摘。\
　　如果你方便的话，可以自己去比奇省\
采摘{10颗忍冬滕=FCO=253}过来，到时候你拿过来我\
帮你制成<如梦丹>就行了。\ \
{任务奖励：=FCO=243}\
　任务物品： \
{12=x}{i.568=item}\ \ \
　声望值： [$任务_教头心事_第八步奖励声望]\
　经验值： [$任务_教头心事_第八步奖励经验]\ \
<&我这就去采些过来/@采集忍冬滕>\
<&结束对话/@exit>\
#ACT
SHOWEFFECT [$特效_步骤完成]
SETMISSION [143] 1
ADDMISSION 0 教头心事第八步

[@采集忍冬滕]
#IF
#SAY
{<$SELFNAME>=FCO=243}：\
　　建议你带上随机，在比奇的树林里搜\
索这类药材。\ \
<&知道了/@exit>\

[@任务_教头心事_第八步完成]
#IF
CHECKMISSION [143] 1
CHECKMISSION [144] 0
CHECKITEMCOUNT 忍冬滕 ? 10
#SAY
{<$SELFNAME>=FCO=243}：\
　　{如梦丹=FCO=250}已经配制好了，现在你可以拿\
去交给<武馆教头/@move(比奇省,324,267)>了。\ \
{任务奖励：=FCO=243}\
　声望值： [$任务_教头心事_第九步奖励声望]\
　经验值： [$任务_教头心事_第九步奖励经验]\
　道具奖励： \
{12=x}{i.498,c.1,d.15,b.0,b.4,b.6,b.7=item}{46=x}{i.542,c.2,b.0,b.4,b.6,b.7=item}{46=x}{i.314,c.2,b.0,b.4,b.6,b.7=item}\ \ \ \
<&多谢，我这就送过去/@move(比奇省,324,267)>\
<&结束对话/@exit>\
#ACT
SHOWEFFECT [$特效_步骤完成]
SETMISSION [144] 1
GIVE 如梦丹 1
TAKECOUNT 忍冬滕 10
CHANGEEXP + [$任务_教头心事_第八步奖励经验]
CREDITPOINT + [$任务_教头心事_第八步奖励声望]
UPDATEMISSION 0 教头心事第九步
}